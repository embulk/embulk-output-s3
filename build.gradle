plugins {
    id "java"
    id "checkstyle"
    id "maven-publish"
//    id "signing"
    id "org.embulk.embulk-plugins" version "0.4.2"
}
repositories {
    mavenCentral()
}

group = "io.github.llibra"
version = "1.5.0"
description = "Stores files on S3."

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-spi:0.10.31"
    compileOnly "org.embulk:embulk-api:0.10.31"

    compile("com.amazonaws:aws-java-sdk-s3:1.11.271") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "joda-time", module: "joda-time"

        // commons-logging api is provided by jcl-over-slf4j below.
        exclude group: "commons-logging", module: "commons-logging"
    }

    compile("org.embulk:embulk-util-config:0.3.0") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }
    compile "org.embulk:embulk-util-file:0.1.3"

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.19.
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"
    compile "joda-time:joda-time:2.9.2"

    testCompile "junit:junit:4.13.2"
    testCompile "org.embulk:embulk-core:0.10.31"
    testCompile "org.embulk:embulk-core:0.10.31:tests"
    testCompile "org.embulk:embulk-spi:0.10.31"
    testCompile "org.embulk:embulk-api:0.10.31"
    //testCompile  "org.embulk:embulk-standards:0.10.31"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    from rootProject.file("LICENSE")
}

sourcesJar {
    from rootProject.file("LICENSE")
}

javadocJar {
    from rootProject.file("LICENSE")
}

//signing {
//    // https://docs.gradle.org/current/userguide/signing_plugin.html#sec:using_gpg_agent
//    useGpgCmd()
//    sign publishing.publications.maven
//}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

embulkPlugin {
    mainClass = "org.embulk.output.S3FileOutputPlugin"
    category = "output"
    type = "s3"
}

gem {
    from("LICENSE.txt")  // Optional -- if you need other files in the gem.
    authors = [ "Manabu Takayama" ]
    email = [ "learn.libra@gmail.com" ]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "S3 file output plugin for Embulk"
    homepage = "https://github.com/llibra/embulk-output-s3"
    licenses = [ "MIT" ]
    //metadata = []  // Optional -- if you need metadata in the gem.
}
gemPush {
    host = "https://rubygems.org"
}
tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:deprecation'
}
